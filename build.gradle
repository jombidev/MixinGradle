plugins {
    id "groovy"
    id "idea"
    id "maven-publish"
    id "com.gradle.plugin-publish" version "0.14.0"
}

group = 'org.spongepowered'
version = buildVersion + (buildType == 'RELEASE' ? '' : "-$buildType")
archivesBaseName = 'mixingradle'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    deployerJars
}

dependencies {
    implementation gradleApi()
    implementation 'com.google.guava:guava:17.0'
    implementation 'org.ow2.asm:asm-debug-all:5.0.3'
}

artifacts {
    archives jar
//    archives groovydocJar
}

processResources {
    from "LICENSE.txt"

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

jar {
    manifest.attributes(
            "Built-By": System.properties['user.name'],
            "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
            "Implementation-Title": name,
            "Implementation-Version": archiveVersion
    )

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    archiveClassifier.set("groovydoc")
}

pluginBundle {
    website = 'https://github.com/xcfrg/MixinGradle'
    vcsUrl = 'https://github.com/xcfrg/MixinGradle'
    description = 'Gradle plugin for SpongePowered Mixin'
    tags = ['spongepowered', 'sponge', 'mixin']
    plugins {
        patcher {
            id = 'org.spongepowered.mixin'
            displayName = 'SpongePowered Mixin Gradle Plugin'
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

publishing {
    repositories {
        maven {
            url = "$buildDir/repository"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'MixinGradle'
                url = 'https://github.com/xcfrg/MixinGradle'

                developers {
                    developer {
                        id = 'mixingradle'
                        name = 'xcfrg'
                    }
                }
            }
        }
    }
}